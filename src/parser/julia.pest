program = { SOI ~ NL* ~ statement_sequence ~ NL* ~ EOI}
atomic_expression = _{ variable | string | character | number | identifiable_operator | parenthetical }
typeable = { block | property | atomic_expression }
non_infix = _{ ret | typed | typeable }
non_tuple = _{ funcall | unary | infix | non_infix}
expression = _{ range | assignment | tuple | non_tuple }
terminator = _{ (SEMICOLON | NL)+ | &EOI}
number = { exponential | float | integer }
integer = @{ "-"? ~ (( "0x" ~ ASCII_HEX_DIGIT+) | ( "0o" ~ ASCII_OCT_DIGIT+) | ( "0b" ~ ASCII_BIN_DIGIT+) | ASCII_DIGIT+) }
float = @{ "-"? ~ ((ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT*) | (ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+))}
exponential = @{ (float | integer) ~ ("e" | "f") ~ integer }
string = @{ "\"" ~ ( "\\\"" | (!"\"" ~ ANY) )* ~ "\"" }
character = @{ "'" ~ "\\"? ~ ANY ~ "'" }
reserved = { BEGIN | BREAK | CATCH | CONST | CONTINUE | DO | ELSE | ELSEIF | END | EXPORT | FALSE | FINALLY | FOR | FUNCTION |
             GLOBAL | IF | IMPORT | LET | LOCAL | MACRO | MODULE | QUOTE | RETURN | STRUCT | TRUE | TRY | USING | WHILE}
variable = @{ !reserved ~ (ASCII_ALPHA | "_" | VAR_UNI) ~ (ASCII_ALPHANUMERIC | "_" | "!" | VAR_UNI)* }
identifiable_operator = { IDENTIFIABLE_ASSIGNMENT | PAIR | COMPARISON | PIPE | COLON | PLUS | TIMES | RATIONAL | BITSHIFT | POWER }
block = _{ block_generic }
statement = { expression ~ terminator }
statement_sequence = { statement+ }
block_body = { statement_sequence? ~ expression? }
block_generic = { BEGIN ~ NL* ~ block_body ~ NL* ~ END}
parenthetical = { LEFT_PAREN ~ block_body ~ RIGHT_PAREN }
dotted_atomic = { DOT ~ atomic_expression}
property = { atomic_expression ~ dotted_atomic+ }
infix_operator = { PAIR | CONDITIONAL | ARROW | LAZY_OR | LAZY_AND | COMPARISON | PIPE|
                   PLUS | TIMES | RATIONAL | BITSHIFT | POWER }
unary_operator = { UNARY_PLUS | UNARY_TIMES | NOT | RADICAL | UNARY_COMPARISON | "¬" }
assignable = _{ (LEFT_PAREN ~ tuple ~ RIGHT_PAREN) | tuple | variable | identifiable_operator }
assignment = { assignable ~ ASSIGNMENT ~ NL* ~ expression }
infix_lhs = { non_infix ~ infix_operator ~ NL* }
infix = { infix_lhs+ ~ non_infix }
range = { non_tuple ~ COLON ~ non_tuple ~ (COLON ~ non_tuple)? }
tuple_lhs = { non_tuple ~ COMMA ~ NL* }
tuple = { tuple_lhs+ ~ non_tuple?}
unary = { unary_operator ~ expression }
ret = { RETURN ~ NL* ~ expression }
typed = { typeable ~ DECL ~ variable }
funcall = { atomic_expression ~ LEFT_PAREN ~ NL* ~ expression ~ NL* ~ RIGHT_PAREN }

// -- Literals -- //
WHITESPACE = _{ (" " | "\t")+ }
NL = _{ "\r"? ~ "\n" }
COMMENT = { "#=" ~ (!"=#" ~ ANY)* ~ "=#" }
SEMICOLON = { ";" }
BAREMODULE = { "baremodule" }
BEGIN = _{ "begin" }
BREAK = { "break" }
CATCH = { "catch" }
CONST = { "const" }
CONTINUE = { "continue" }
DO = { "do" }
ELSE = { "else" }
ELSEIF = { "elseif" }
END = _{ "end" }
EXPORT = { "export" }
FALSE = { "false" }
FINALLY = { "finally" }
FOR = { "for" }
FUNCTION = { "function" }
GLOBAL = { "global" }
IF = { "if" }
IMPORT = { "import" }
LET = { "let" }
LOCAL = { "local" }
MACRO = { "macro" }
MODULE = { "module" }
QUOTE = { "quote" }
RETURN = { "return" }
STRUCT = { "struct" }
TRUE = { "true" }
TRY = { "try" }
USING = { "using" }
WHILE = { "while" }
VAR_UNI = { '\u{FF}'..'\u{10FFFF}' }  // The actual valid unicode range is much less, but short of including every codepoint here, we won't worry about it

// -- Operators -- //
BROADCASTABLE_ASSIGNMENT = { "="  | "+=" | "-=" | "−=" | "*="   | "/="   | "//=" | "\\\\=" | "^=" |
                             "÷=" | "%=" | "<<=" | ">>=" | ">>>=" | "|=" | "&=" | "⊻=" }
IDENTIFIABLE_ASSIGNMENT = {"≔" | "⩴" | "≕" | "~"}
ASSIGNMENT = { (DOT? ~ (BROADCASTABLE_ASSIGNMENT | IDENTIFIABLE_ASSIGNMENT)) | ":=" | "$=" }
PAIR = { "=>" }
CONDITIONAL = { "?" }
ARROW = { DOT? ~ "←" | "→" | "↔" | "↚" | "↛" | "↞" | "↠" | "↢" | "↣" | "↦" | "↤" | "↮" | "⇎" | "⇍" | "⇏" | "⇐" | "⇒" | "⇔" |
                "⇴" | "⇶" | "⇷" | "⇸" | "⇹" | "⇺" | "⇻" | "⇼" | "⇽" | "⇾" | "⇿" | "⟵" | "⟶" | "⟷" | "⟹" | "⟺" | "⟻" |
                "⟼" | "⟽" | "⟾" | "⟿" | "⤀" | "⤁" | "⤂" | "⤃" | "⤄" | "⤅" | "⤆" | "⤇" | "⤌" | "⤍" | "⤎" | "⤏" | "⤐" | "⤑" |
                "⤔" | "⤕" | "⤖" | "⤗" | "⤘" | "⤝" | "⤞" | "⤟" | "⤠" | "⥄" | "⥅" | "⥆" | "⥇" | "⥈" | "⥊" | "⥋" | "⥎" | "⥐" |
                "⥒" | "⥓" | "⥖" | "⥗" | "⥚" | "⥛" | "⥞" | "⥟" | "⥢" | "⥤" | "⥦" | "⥧" | "⥨" | "⥩" | "⥪" | "⥫" | "⥬" | "⥭" |
                "⥰" | "⧴" | "⬱" | "⬰" | "⬲" | "⬳" | "⬴" | "⬵" | "⬶" | "⬷" | "⬸" | "⬹" | "⬺" | "⬻" | "⬼" | "⬽" | "⬾" | "⬿" |
                "⭀" | "⭁" | "⭂" | "⭃" | "⭄" | "⭇" | "⭈" | "⭉" | "⭊" | "⭋" | "⭌" | "￩" | "￫" | "⇜" | "⇝" | "↜" | "↝" | "↩" |
                "↪" |"↫" | "↬" | "↼" | "↽" | "⇀" | "⇁" | "⇄" | "⇆" | "⇇" | "⇉" | "⇋" | "⇌" | "⇚" | "⇛" | "⇠" | "⇢" | "↷" |
                "↶" | "↺" | "↻" | "-->" | "<--" | "<-->" }
LAZY_OR = { DOT? ~ "||" }
LAZY_AND = { DOT? ~ "&&" }
NOT = { DOT? ~ "!" }
BROADCASTABLE_COMPARISON = { ">" | "<" | ">=" | "≥" | "<=" | "≤" | "==" | "===" | "≡" | "!=" | "≠" | "!==" | "≢" | "∈" |
                             "∉" | "∋" | "∌" | "⊆" | "⊈" | "⊂" | "⊄" | "⊊" | "∝" | "∊" | "∍" | "∥" | "∦" | "∷" | "∺" | "∻" |
                             "∽" | "∾" | "≁" | "≃" | "≂" | "≄" | "≅" | "≆" | "≇" | "≈" | "≉" | "≊" | "≋" | "≌" | "≍" | "≎" |
                             "≐" | "≑" | "≒" | "≓" | "≖" | "≗" | "≘" | "≙" | "≚" | "≛" | "≜" | "≝" | "≞" | "≟" | "≣" | "≦" |
                             "≧" | "≨" | "≩" | "≪" | "≫" | "≬" | "≭" | "≮" | "≯" | "≰" | "≱" | "≲" | "≳" | "≴" | "≵" | "≶" |
                             "≷" | "≸" | "≹" | "≺" | "≻" | "≼" | "≽" | "≾" | "≿" | "⊀" | "⊁" | "⊃" | "⊅" | "⊇" | "⊉" | "⊋" |
                             "⊏" | "⊐" | "⊑" | "⊒" | "⊜" | "⊩" | "⊬" | "⊮" | "⊰" | "⊱" | "⊲" | "⊳" | "⊴" | "⊵" | "⊶" | "⊷" |
                             "⋍" | "⋐" | "⋑" | "⋕" | "⋖" | "⋗" | "⋘" | "⋙" | "⋚" | "⋛" | "⋜" | "⋝" | "⋞" | "⋟" | "⋠" | "⋡" |
                             "⋢" | "⋣" | "⋤" | "⋥" | "⋦" | "⋧" | "⋨" | "⋩" | "⋪" | "⋫" | "⋬" | "⋭" | "⋲" | "⋳" | "⋴" | "⋵" |
                             "⋶" | "⋷" | "⋸" | "⋹" | "⋺" | "⋻" | "⋼" | "⋽" | "⋾" | "⋿" | "⟈" | "⟉" | "⟒" | "⦷" | "⧀" | "⧁" |
                             "⧡" | "⧣" | "⧤" | "⧥" | "⩦" | "⩧" | "⩪" | "⩫" | "⩬" | "⩭" | "⩮" | "⩯" | "⩰" | "⩱" | "⩲" | "⩳" |
                             "⩵" | "⩶" | "⩷" | "⩸" | "⩹" | "⩺" | "⩻" | "⩼" | "⩽" | "⩾" | "⩿" | "⪀" | "⪁" | "⪂" | "⪃" | "⪄" |
                             "⪅" | "⪆" | "⪇" | "⪈" | "⪉" | "⪊" | "⪋" | "⪌" | "⪍" | "⪎" | "⪏" | "⪐" | "⪑" | "⪒" | "⪓" | "⪔" |
                             "⪕" | "⪖" | "⪗" | "⪘" | "⪙" | "⪚" | "⪛" | "⪜" | "⪝" | "⪞" | "⪟" | "⪠" | "⪡" | "⪢" | "⪣" | "⪤" |
                             "⪥" | "⪦" | "⪧" | "⪨" | "⪩" | "⪪" | "⪫" | "⪬" | "⪭" | "⪮" | "⪯" | "⪰" | "⪱" | "⪲" | "⪳" | "⪴" |
                             "⪵" | "⪶" | "⪷" | "⪸" | "⪹" | "⪺" | "⪻" | "⪼" | "⪽" | "⪾" | "⪿" | "⫀" | "⫁" | "⫂" | "⫃" | "⫄" |
                             "⫅" | "⫆" | "⫇" | "⫈" | "⫉" | "⫊" | "⫋" | "⫌" | "⫍" | "⫎" | "⫏" | "⫐" | "⫑" | "⫒" | "⫓" | "⫔" |
                             "⫕" | "⫖" | "⫗" | "⫘" | "⫙" | "⫷" | "⫸" | "⫹" | "⫺" | "⊢" | "⊣" | "⟂" | "⫪" | "⫫" | "<:" | ">:" }
COMPARISON = { (DOT? ~ BROADCASTABLE_COMPARISON) | "in" | "isa" }
UNARY_COMPARISON = { DOT? ~ "~"}

PIPE = { DOT? ~ ( "<|" | "|>" ) }
BROADCASTABLE_COLON = { "…" | "⁝" | "⋮" | "⋱" | "⋰" | "⋯" }
COLON = { (DOT? ~ BROADCASTABLE_COLON) | ":" | ".." }
BROADCASTABLE_PLUS = { "−" | "¦" | "\\" | "⊕" | "⊖" | "⊞" | "⊟" | "++" | "∪" | "∨" | "⊔" | "∔" | "∸" |
                       "≏" | "⊎" | "⊻" | "⊽" | "⋎" | "⋓" | "⧺" | "⧻" | "⨈" | "⨢" | "⨣" | "⨤" | "⨥" | "⨦" | "⨧" | "⨨" | "⨩" |
                       "⨪" | "⨫" | "⨬" | "⨭" | "⨮" | "⨹" | "⨺" | "⩁" | "⩂" | "⩅" | "⩊" | "⩌" | "⩏" | "⩐" | "⩒" | "⩔" | "⩖" |
                       "⩗" | "⩛" | "⩝" | "⩡" | "⩢" | "⩣" }
UNARY_BROADCASTABLE_PLUS = { "+" | "-" | "±" | "∓"}
PLUS = { (DOT? ~ (BROADCASTABLE_PLUS | UNARY_BROADCASTABLE_PLUS)) | "$" }
UNARY_PLUS = { (DOT? ~ UNARY_BROADCASTABLE_PLUS) | "$" }
TIMES = { DOT? ~ ("*" | "/" | "⌿" | "÷" | "%" | "&" | "·" | "·" | "⋅" | "∘" | "×" | "\\\\" | "∩" | "∧" | "⊗" | "⊘" | "⊙" | "⊚" |
                  "⊛" | "⊠" | "⊡" | "⊓" | "∗" | "∙" | "∤" | "⅋" | "≀" | "⊼" | "⋄" | "⋆" | "⋇" | "⋉" | "⋊" | "⋋" | "⋌" | "⋏" | "⋒" |
                  "⟑" | "⦸" | "⦼" | "⦾" | "⦿" | "⧶" | "⧷" | "⨇" | "⨰" | "⨱" | "⨲" | "⨳" | "⨴" | "⨵" | "⨶" | "⨷" | "⨸" | "⨻" | "⨼" |
                  "⨽" | "⩀" | "⩃" | "⩄" | "⩋" | "⩍" | "⩎" | "⩑" | "⩓" | "⩕" | "⩘" | "⩚" | "⩜" | "⩞" | "⩟" | "⩠" | "⫛" | "⊍" | "▷" |
                  "⨝" | "⟕" | "⟖" | "⟗" | "⨟") }
UNARY_TIMES = { DOT? ~ "*"}
RATIONAL = { DOT? ~ "//" }
BITSHIFT = { DOT? ~ ("<<" | ">>" | ">>>")}
POWER = { DOT? ~ ("^" | "↑" | "↓" | "⇵" | "⟰" | "⟱" | "⤈" | "⤉" | "⤊" | "⤋" | "⤒" | "⤓" | "⥉" | "⥌" | "⥍" | "⥏" | "⥑" | "⥔" | "⥕" |
                  "⥘" | "⥙" | "⥜" | "⥝" | "⥠" | "⥡" | "⥣" | "⥥" | "⥮" | "⥯" | "￪" | "￬") }
DECL = { "::" }
DOT = { "." }
TRANS = { ".\'" }
CTRANS = { "\'" }
VARARG = { "..." }
AT = { "@" }
SUBTYPE = { "<:" }
SUPERTYPE = { ">:" }
RADICAL = { DOT? ~ ("√" | "∛" | "∜")}

// --- Syntax --- //
LEFT_PAREN = _{ "(" }
RIGHT_PAREN = _{ ")" }
LEFT_BRACKET = { "[" }
RIGHT_BRACKET = { "]"}
LEFT_BRACE = { "{" }
RIGHT_BRACE = { "}" }
COMMA = { "," }
